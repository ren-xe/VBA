
Sub RemoveTheStyles()
    Dim style As Style, i As Long, count As Long
    On Error Resume Next
    If ActiveWorkbook.MultiUserEditing Then
        If MsgBox("You cannot remove Styles in a Shared workbook." & vbCr & vbCr & _
                  "Do you want to unshare the workbook?", vbYesNo + vbInformation) = vbYes Then
            ActiveWorkbook.ExclusiveAccess
            If Err.Number = 1004 Then
                Exit Sub
            End If
        Else
            Exit Sub
        End If
    End If
    count = ActiveWorkbook.Styles.Count
    Application.ScreenUpdating = False
    For i = count To 1 Step -1
        If i Mod 600 = 0 Then DoEvents
        Set style = ActiveWorkbook.Styles(i)
        Application.StatusBar = "Deleting " & count - i + 1 & " of " & count & " " & style.Name
        If Not style.BuiltIn Then
            style.Delete
            If Err.Number <> 0 Then
                MsgBox Err.Description, vbExclamation, "Remove The Styles AddIn"
                Exit For
            End If
        End If
    Next
    Application.ScreenUpdating = True
    Application.StatusBar = False
    MsgBox "completed !!"
End Sub
